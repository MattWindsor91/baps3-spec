= Message Protocol

:shell:    http://pubs.opengroup.org/onlinepubs/009604599/utilities/xcu_chap02.html
:isspace:  http://pubs.opengroup.org/onlinepubs/009695399/functions/isspace.html
:UTF-8:    http://www.ietf.org/rfc/rfc3629.txt
:tests:    link:../tests/msgproto.adoc

The Bifrost _message protocol_ is the base protocol Bifrost servers and
clients use to communicate.  It is designed to be:
criteria:

Composable::
  The API is intended to allow separate implementations of single
  services to be composed into a single logical service, by connecting
  servers together through the API.  Responses and requests not
  intended for a server with upstream and downstream connections
  respectively can be _forwarded_ transparently along the API
  connections;
Transparent::
  The API is text-based, and can be inspected by humans using either
  TCP/IP packet sniffing or connecting directly to services to be
  inspected;
Simple::
  The API uses a minimalistic line-orientated protocol, in which
  each line is comprised of one or more words, and uses shell-style
  word-escaping conventions (as well as C-style backslash escapes).
  It is relatively simple to implement, requires little to no
  external dependencies, and is modest in its usage of network
  bandwidth.

== Design

The message protocol is based upon {shell}[POSIX shell] conventions. This is
because:

* It is lightweight and relatively easy to parse;
* It is a good match for the command-and-arguments style of the protocol layer
on top;
  API;
* The shell style of escaping is convenient for escaping paths
  (especially on Windows, where the single-quote syntax can avoid
  needing to escape every backslash in a Windows path).

A disadvantage is that it is impossible to perform a context-free
splitting of input into lines; the line-feed character could be
escaped.  However, since the usual transport used by the message
protocol is TCP, and most TCP libraries provide unbuffered input in
chunks which may be smaller or larger than one line, this is not a
major issue.

== Encoding

The internal API is designed to expect requests and responses in
the {UTF-8}[UTF-8] encoding.  However, to allow simple implementations
that operate on a per-byte level, all characters with special meaning
are taken from the single-byte UTF-8 subset (that is, ASCII).  In
addition, the backslash-escape mechanism (see below) operates only on
one byte.

API users *must* support the sending and receiving of all single-byte
UTF-8 character codes (that is, ASCII), and *should* support the
sending and receiving of the full UTF-8 range subject to the
limitations below.

While a message is in transit, API users *must not* alter its bytes in
any way other than changing between equivalent quoting, escaping and
line/word-delimiting forms.  This includes correcting, omitting, or
replacing any non-UTF-8 byte sequences, as well as case folding or
normalising.  However, changing from double quotes to single quotes, or
introducing or removing more whitespace to an inter-word whitespace
run, is ok.

== Overview

The message protocol uses the linefeed-delimited _commands_, each
containing one or more whitespace-delimited
_words_.  Each word consists of a sequence of bytes (usually
representing UTF-8 characters).

=== Quoting and escaping
To allow all byte sequences to be represented in words, the protocol
uses several quoting and escaping strategies, which are summarised in
the below table and formalised later.

Note that, while each quoting mechanism lasts until the matching (and
non-backslash-escaped) quote is found, backslash-escape affects only
the following byte (then, the previous quoting behaviour returns
afterwards).

Also note that backslash-escape only escapes the next _byte_: for this
reason, clients and services *should not* backslash-escape multi-byte
UTF-8 characters (such treatment is unnecessary, as all special
characters are single-byte).

Finally, backslashes *must not* be interpreted as starting an escape
while in backslash-escape or double-quoted modes, and *must* be
emitted verbatim.

[cols="1,1,1,1,1", options="header"]
.Quoting and escaping strategies
|===
|Character
|Meaning when unquoted
|Meaning when single-quoted
|Meaning when double-quoted
|Meaning when backslash-escaped

|`'` (0x27)
|Begins single-quoted mode
|Begins unquoted mode
|`'`
|`'`

|`"` (0x22)
|Begins double-quoted mode
|`"`
|Ends double-quoted mode
|`"`

|`\` (0x5C)
|Begins backslash escape for next byte only
|`\`
|Begins backslash escape for next byte only
|`\`

|Any other byte 'b'
|'b'
|'b'
|'b'
|'b'
|===


== Formal Specification

The message protocol adheres to the following BNF grammar:

--------
<command>     ::= <words> "\n"
               |  <whitespace> <words> "\n"
               |  <words> <whitespace> "\n"
               |  <whitespace> <words> <whitespace> "\n"

<words>       ::= <word>
               |  <word> <whitespace> <words>

<word>        ::= "'" <single>
               |  "'" <single> <word>
               |  "\"" <double>
               |  "\"" <double> <word>
               | <escape>
               | <escape> <word>
               | <normal-char>
               | <normal-char> <word>

<single>      ::= "'"
               |  <single-char> <single>

<double>      ::= "\""
               |  <escape> <double>
               |  <double-char> <double>

<whitespace>  ::= <ws-char>
               |  <ws-char> <whitespace>

<escape>      ::= "\\" <any-char>

<ws-char>     ::= " " | "\t" | "\v" | "\f" | "\r"
<normal-char> ::= (any byte not in <ws-char> or one of " ' \ \n)
<double-char> ::= (any byte except one of " \)
<single-char> ::= (any byte except ')
<any-char>    ::= (any byte)
--------

Each distinct `word` and `command` production denotes a separate word
and command respectively.  Any `normal-char`, `double-char`,
`single-char`, or `any-char` is emitted verbatim in its word.

Syntax errors (for example, a line containing no words) *must* cause a
`WHAT` response to be sent.

Escape sequences in the above BNF may be interpreted as follows:
[cols="1,1,2", options="header"]
.BNF escape sequences
|===
|Sequence
|Byte
|Meaning

|`\t`
|`0x09`
|Horizontal tab

|`\n`
|`0x0A`
|Line feed

|`\v`
|`0x0B`
|Vertical tab

|`\f`
|`0x0C`
|Form feed

|`\r`
|`0x0B`
|Carriage return

|`\\`
|`0x5C`
|Backslash
|===

=== Examples

See the Internal API {tests}[compliance tests].
